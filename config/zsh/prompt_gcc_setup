function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]=' %F{red}๐%'
  fi
}

function prompt_gcc_precmd {
  vcs_info
}

function prompt_gcc_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_gcc_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{magenta}ﬦ'
  zstyle ':prezto:module:editor:info:keymap:primary:insert' format 'I'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format 'O'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{red}ﬦ'
  zstyle ':prezto:module:editor:info:completing' format '...'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr ' %F{green}๏%f'
  zstyle ':vcs_info:*' unstagedstr ' %F{yellow}๐%f'
  zstyle ':vcs_info:*' formats ' %b%c%u'
  zstyle ':vcs_info:*' actionformats " - %b%c%u|%F{cyan}%a%f"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  # Define prompts.
  PROMPT='%2~ $editor_info[keymap] '
  RPROMPT='${vcs_info_msg_0_}'
}

function prompt_gcc_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'gcc'
}

prompt_gcc_setup "$@"
# vim: ft=zsh
